import random
import options

var rng: Option[Rand] = none(Rand)

proc initRand*(seed: int) =
  ## Initialize random number generator using given `seed`.
  ## This procedure should be called before other procedures or templates 
  ## in this module are called.
  rng = some(random.initRand(seed))

proc rand*[T: int or float](lb, ub: T): T =
  ## Returns random number between `lb` and `ub` (including `lb` and `ub`).
  rng.get.rand(lb..ub)

proc shuffle*[T](xs: seq[T]): seq[T] =
  ## Returns a sequence which was generated by shuffling `xs`.
  var ys = xs
  rng.get.shuffle(ys)
  ys

template withProbability*(prob: float, body: untyped): untyped =
  ## With probability `prob`, do `body`.
  if rand(0.0, 1.0) <= prob:
    body